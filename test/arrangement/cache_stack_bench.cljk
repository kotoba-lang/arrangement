(ns arrangement.cache-stack-bench
  "What the cache stack buys, composed rather than built in.

  Each layer is a separate library and each is added by WRAPPING, not by
  changing the engine:

    block-cache   dedupes the block reads under a scan. Key is a CID, so it
                  can never go stale.
    datom-source  `cached` dedupes the scans themselves. A pattern is NOT a
                  version, so this one is scoped to a query.

  Read the BLOCKS column, not the ms column. The store here is an in-memory
  map, so a block read costs nothing and the block cache CANNOT show a time
  win -- it can only add bookkeeping. Its payoff is proportional to what a
  block read actually costs, which in production is an R2/D1 round trip. The
  timings are single runs and noisy; the k=160 block-cache row has been seen
  slower than baseline, which is what measuring an optimisation against a
  cost that is not there looks like.

    clojure -M:cache-bench
  "
  (:require [arrangement.core :as arr]
[arrangement.datalog :as dl]
            [arrangement.source :as as]
            [datom.source :as ds]
            [block.cache :as bc]
            [ipld.core :as ipld]))
(def blind #(str "b|" (pr-str %))) (def enc identity) (def dec- identity)
(def yes (constantly true))
(def reach '{:find [?b] :in [?a] :where [(reaches ?a ?b)]
             :rules [[(reaches ?x ?y) [?x "next" ?y]]
                     [(reaches ?x ?y) [?x "next" ?z] (reaches ?z ?y)]]})
(defn build [k]
  (let [store (atom {}) reads (atom 0)
        put! (fn [cid b] (swap! store assoc cid b))
        raw (fn [cid] (swap! reads inc) (get @store cid))
        cid (arr/commit! put! (reduce arr/assert-quad (arr/empty-db)
                                      (for [i (range k)]
                                        {:s (str "n" i) :p "next" :o (str "n" (inc i))}))
                         nil arr/current-schema-version blind enc)]
    {:raw raw :reads reads :cid cid}))
(defn run [label k make]
  (let [{:keys [raw reads cid]} (build k)]
    (reset! reads 0)
    (let [src (make raw cid)
          t0 (System/nanoTime)
          r (dl/q src reach yes ["n0"])
          ms (Math/round (double (/ (- (System/nanoTime) t0) 1e6)))]
      {:label label :k k :blocks @reads :ms ms :answers (count r)})))
(def verify! (fn [cid bytes] (when-not (= cid (ipld/cid bytes))
                               (throw (ex-info "cid mismatch" {})))))
(defn -main [& _]
  (println (format "%-34s %4s %8s %6s %8s" "composition" "k" "blocks" "ms" "answers"))
(doseq [k [40 80 160]]
  (doseq [[label make]
          [["cursor (baseline)"
            (fn [raw cid] (as/cursor raw cid blind dec-))]
           ["cursor + block-cache"
            (fn [raw cid] (as/cursor (bc/wrap-get-fn (bc/memory (* 8 1024 1024)) raw
                                                     {:verify! verify!})
                                     cid blind dec-))]
           ["cursor + scan-cache"
            (fn [raw cid] (ds/cached (as/cursor raw cid blind dec-)))]
           ["cursor + block + scan cache"
            (fn [raw cid] (ds/cached (as/cursor (bc/wrap-get-fn (bc/memory (* 8 1024 1024)) raw
                                                                {:verify! verify!})
                                                cid blind dec-)))]]]
    (let [{:keys [blocks ms answers]} (run label k make)]
      (println (format "%-34s %4d %8d %6d %8d" label k blocks ms answers))))
  (println))
  (shutdown-agents))
